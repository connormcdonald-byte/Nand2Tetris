/**
 * The ALU (Arithmetic Logic Unit).
 * Computes one of the following functions:
 * x+y, x-y, y-x, 0, 1, -1, x, y, -x, -y, !x, !y,
 * x+1, y+1, x-1, y-1, x&y, x|y on two 16-bit inputs, 
 * according to 6 input bits denoted zx,nx,zy,ny,f,no.
 * In addition, the ALU computes two 1-bit outputs:
 * if the ALU output == 0, zr is set to 1; otherwise zr is set to 0;
 * if the ALU output < 0, ng is set to 1; otherwise ng is set to 0.
 */

// Implementation: the ALU logic manipulates the x and y inputs
// and operates on the resulting values, as follows:
// if (zx == 1) set x = 0        // 16-bit constant
// if (nx == 1) set x = !x       // bitwise not
// if (zy == 1) set y = 0        // 16-bit constant
// if (ny == 1) set y = !y       // bitwise not
// if (f == 1)  set out = x + y  // integer 2's complement addition
// if (f == 0)  set out = x & y  // bitwise and
// if (no == 1) set out = !out   // bitwise not
// if (out == 0) set zr = 1
// if (out < 0) set ng = 1

CHIP ALU {
    IN  
        x[16], y[16],  // 16-bit inputs        
        zx, // zero the x input?
        nx, // negate the x input?
        zy, // zero the y input?
        ny, // negate the y input?
        f,  // compute out = x + y (if 1) or x & y (if 0)
        no; // negate the out output?

    OUT 
        out[16], // 16-bit output
        zr, // 1 if (out == 0), 0 otherwise
        ng; // 1 if (out < 0),  0 otherwise

    PARTS:
    And16(a = x, b = false, out = zerox);
    Mux16(a = x, b = zerox, sel = zx, out = xAndNotzx);
    Not16(in = xAndNotzx, out = NotxAndNotzx);
    Mux16(a = xAndNotzx, b = NotxAndNotzx, sel = nx, out = xout);
    And16(a = y, b = false, out = zeroy);
    Mux16(a = y, b = zeroy, sel = zy, out = yAndNotzy);
    Not16(in = yAndNotzy, out = NotyAndNotzy);
    Mux16(a = yAndNotzy, b = NotyAndNotzy, sel = ny, out = yout);
    Add16(a = xout, b = yout, out = preout2);
    And16(a = xout, b = yout, out = preout1);
    Mux16(a = preout1, b = preout2, sel = f, out = almostout);
    Not16(in = almostout, out = Notalmostout);
    Mux16(a = almostout, b = Notalmostout, sel = no, out = out, out[15] = ng, out[0..7] = temp1, out[8..15] = temp2);
    Or8Way(in = temp1, out = check1);
    Or8Way(in = temp2, out = check2);
    Or(a = check1, b = check2, out = temp3);
    Not(in = temp3, out = zr);
}